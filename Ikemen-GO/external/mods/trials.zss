#===============================================================================
# Trials Mode global code
#===============================================================================
# _iksys_trialsDummyControl: 0 - cooperative, 1 - ai, 2 - manual
# _iksys_trialsGuardMode: 0 - none, 1 - auto, 2 - all, 3 - random
# _iksys_trialsFallRecovery: 0 - none, 1 - ground, 2 - air, 3 - random
# _iksys_trialsDummyMode: 0 - stand, 1 - crouch, 2 - jump, 3 - wjump
# _iksys_trialsDistance: 0 - any, 1 - close, 2 - medium, 3 - far
# _iksys_trialsButtonJam: 0 - none, 1-9 - a/b/c/x/y/z/s/d/w
#===============================================================================

[Function TrialsReposition()]
if map(_iksys_trialsReposition) != 0 {
    Camera{view: free}
    posSet{
        x: map(_iksys_trialsPlayerPosX);
        y: map(_iksys_trialsPlayerPosY);
        redirectid: player(1), id
    }
    posSet{
        x: map(_iksys_trialsDummyPosX);
        y: map(_iksys_trialsDummyPosY);
        redirectid: player(2), id
    }
}

[Function TrialsResetCamera()]
Camera{view: fighting}

#===============================================================================
# Global states (not halted by Pause/SuperPause)
#===============================================================================
[StateDef -4]

ignoreHitPause if gameMode = "trials" {

    # Round start reset
    if roundState = 0 {
        powerSet{value: powerMax}
        map(_iksys_trialsLifeTimer) := 0;
        map(_iksys_trialsPowerTimer) := 0;
        map(_iksys_trialsReposition) := 0;
    }

    # Life and Power recovery
    if !isHelper {
        # Life timer
        if moveType = H || dizzy {
            mapSet{map: "_iksys_trialsLifeTimer"; value: 0}
        } else {
            mapAdd{map: "_iksys_trialsLifeTimer"; value: 1}
        }
        if map(_iksys_trialsLifeTimer) >= 60 {
            if map(_iksys_trialsSetLife) > 0 {
                lifeSet{value: map(_iksys_trialsSetLife)}
                redLifeSet{value: map(_iksys_trialsSetLife)}
            } else {
                lifeSet{value: lifeMax}
                redLifeSet{value: lifeMax}
            }
            mapSet{map: "_iksys_trialsLifeTimer"; value: 0}
        }

        # Power timer
        if ctrl = 0 {
            mapSet{map: "_iksys_trialsPowerTimer"; value: 0}
        } else {
            mapAdd{map: "_iksys_trialsPowerTimer"; value: 1}
        }
        if map(_iksys_trialsPowerTimer) >= 60 {
            powerSet{value: powerMax}
            mapSet{map: "_iksys_trialsPowerTimer"; value: 0}
        }
    }

    # Disable normal KO behavior
    assertSpecial{flag: globalNoKo}

    # Force players out of KO state
    if stateNo = 5150 && time >= 90 && alive {
        selfState{value: 5120}
    }

    # Dummy logic
    if teamSide = 2 && !isHelper {

        # Round start reset
        if roundState = 0 {
            map(_iksys_trialsAirJumpNum) := 0;
            map(_iksys_trialsButtonJam) := 0;
            map(_iksys_trialsDirection) := 0;
            map(_iksys_trialsDistance) := 0;
            map(_iksys_trialsDummyControl) := 0;
            map(_iksys_trialsDummyMode) := 0;
            map(_iksys_trialsFallRecovery) := 0;
            map(_iksys_trialsGuardMode) := 0;
        }

        if roundState = 2 {
            # Dummy Control: Cooperative
            if aiLevel = 0 && map(_iksys_trialsDummyControl) = 0 {

                # Guard mode
                if map(_iksys_trialsGuardMode) = 3 && random < 500 {
                    assertSpecial{flag: autoGuard}
                } else if map(_iksys_trialsGuardMode) = 2 {
                    assertSpecial{flag: autoGuard}
                } else if map(_iksys_trialsGuardMode) = 1 {
                    if moveType = H || stateNo = const(StateDownedGetHit_gettingUp) {
                        map(_iksys_trialsAutoGuardTimer) := 15;
                    } else {
                        map(_iksys_trialsAutoGuardTimer) := (map(_iksys_trialsAutoGuardTimer) - 1);
                    }
                    if map(_iksys_trialsAutoGuardTimer) > 0 {
                        assertSpecial{flag: autoGuard}
                    }
                }

                # Fall Recovery
                if map(_iksys_trialsFallRecovery) != 0 {
                    if stateNo != [const(StateAirGetHit_fallRecoveryOnGroundStillFalling), const(StateAirGetHit_fallRecoveryInAir)] {
                        if moveType = H && stateType = A && hitFall && !getHitVar(isbound) && (pos y || vel y) {
                            let rcv = 0;
                            if vel y > 0 && pos y >= const(movement.air.gethit.groundrecover.ground.threshold) && map(_iksys_trialsFallRecovery) = 1 {
                                let rcv = 1;
                            } else if map(_iksys_trialsFallRecovery) = 2 {
                                let rcv = 1;
                            } else if map(_iksys_trialsFallRecovery) = 3 && random < 100 {
                                let rcv = 1;
                            }

                            if $rcv {
                                if gametime % 2 {
                                    assertInput{flag: x; flag2: y; flag3: z}
                                } else {
                                    assertInput{flag: a; flag2: b; flag3: c}
                                }
                                # Random direction
                                if map(_iksys_trialsFallRecovery) = 3 {
                                    if random < 333 {
                                        assertInput{flag: L}
                                    } else if random < 500 {
                                        assertInput{flag: R}
                                    }
                                    if random < 333 {
                                        assertInput{flag: U}
                                    } else if random < 500 {
                                        assertInput{flag: D}
                                    }
                                }
                            }
                        }
                    }
                }

                # Distance & dummy movement
                let dir = 0;
                if map(_iksys_trialsDistance) != 0 {
                    if map(_iksys_trialsDistance) = 1 {
                        if p2BodyDist x > const240p(10) {
                            let dir = 1;
                            map(_iksys_trialsDirection) := 1;
                        } else if p2BodyDist x < -(const(size.ground.front) + const(size.ground.back) + const240p(10)) {
                            let dir = -1;
                            map(_iksys_trialsDirection) := -1;
                        }
                    } else if map(_iksys_trialsDistance) = 2 {
                        if p2BodyDist x > const240p(130) {
                            let dir = 1;
                            map(_iksys_trialsDirection) := 1;
                        } else if p2BodyDist x < const240p(80) && backEdgeBodyDist > const240p(10) {
                            let dir = -1;
                            map(_iksys_trialsDirection) := -1;
                        }
                    } else if map(_iksys_trialsDistance) = 3 {
                        if p2BodyDist x < const240p(260) && backEdgeBodyDist > const240p(10) {
                            let dir = -1;
                            map(_iksys_trialsDirection) := -1;
                        }
                    }
                }

                # Dummy direction
                if map(_iksys_trialsDirection) != 0 {
                    if $dir = 0 {
                        if vel x * p2,vel x >= 0 || backEdgeBodyDist = 0 || p2,backEdgeBodyDist = 0 {
                            map(_iksys_trialsDirection) := 0;
                        }
                    }
                    # if dummy should move forward and player is not trying to move dummy back
                    if map(_iksys_trialsDirection) = 1 && command != "holdback" {
                        if facing = 1 {
                            assertInput{flag: R}
                        } else {
                            assertInput{flag: L}
                        }
                    # if dummy should move backward and player is not trying to move dummy forward
                    } else if map(_iksys_trialsDirection) = -1 && command != "holdfwd" {
                        if facing = 1 {
                            assertInput{flag: L}
                        } else {
                            assertInput{flag: R}
                        }
                    }
                }

                # Dummy modes
                switch map(_iksys_trialsDummyMode) {
                    case 1:
                        assertInput{flag: D}
                    case 2:
                        assertInput{flag: U}
                    case 3:
                        if vel y >= 0 {
                            assertInput{flag: U}
                        }
                    default:
                        # do nothing
                }

                # Button jam
                if map(_iksys_trialsButtonJam) > 0 {
                    if map(_iksys_trialsButtonJamDelay) > 0 {
                        mapAdd{map: "_iksys_trialsButtonJamDelay"; value: -1}
                    } else {
                        map(_iksys_trialsButtonJamDelay) := 11;
                        switch map(_iksys_trialsButtonJam) {
                            case 1:
                                assertInput{flag: a}
                            case 2:
                                assertInput{flag: b}
                            case 3:
                                assertInput{flag: c}
                            case 4:
                                assertInput{flag: x}
                            case 5:
                                assertInput{flag: y}
                            case 6:
                                assertInput{flag: z}
                            case 7:
                                assertInput{flag: s}
                            case 8:
                                assertInput{flag: d}
                            case 9:
                                assertInput{flag: w}
                            default:
                                map(_iksys_trialsButtonJamDelay) := 0;
                        }
                    }
                }

                # Reposition & camera reset
                if map(_iksys_trialsReposition) != 0 { call TrialsReposition(); map(_iksys_trialsReposition) := 0; }
                if map(_iksys_trialsCameraReset) != 0 { call TrialsResetCamera(); map(_iksys_trialsCameraReset) := 0; }

            }
        }
    }
}
